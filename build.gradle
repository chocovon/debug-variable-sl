plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'com.github.chocovon'
version '2.4.0'

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2024.2.3'

        bundledPlugin 'com.intellij.java'

        instrumentationTools()
    }
}

patchPluginXml {
    sinceBuild = '193'
    untilBuild = '251'

    changeNotes = """
      2.4.0 fixed problems with scrolling, other bug fixes <br/>
      2.3.0 added more options to code generator <br/>
      2.2.0 added code folding support in the dialog <br/>
      2.1.0 added extracting variable as JSON to the dialog
      """
}

task copyPluginXml (type: Copy) {
    from "src/configurations/meta-inf/" + rootProject.name
    into "src/main/resources/META-INF"
    include "**"
}

// set buildSearchableOptions=false in settings.properties to disable slow buildSearchableOptions while developing.
if (gradle.buildSearchableOptions) {
    buildSearchableOptions.enabled = false
}

task dvslBuild(type: GradleBuild) {
    tasks = ['copyPluginXml', ':inner-tool:buildInnerTool', 'buildPlugin']
}
