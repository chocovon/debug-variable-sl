plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'com.github.chocovon'
version '2.4.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation files("${System.getProperty("java.home")}/../lib/tools.jar")
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.2.3'
    plugins = ['java', 'android']
}
patchPluginXml {
    sinceBuild(193)
    untilBuild(251)

    changeNotes """
      2.4.0 fixed problems with scrolling, other bug fixes <br/>
      2.3.0 added more options to code generator <br/>
      2.2.0 added code folding support in the dialog <br/>
      2.1.0 added extracting variable as JSON to the dialog
      """
}

task copyPluginXml (type: Copy) {
    from "src/configurations/meta-inf/" + rootProject.name
    into "src/main/resources/META-INF"
    include "**"
}

// set buildSearchableOptions=false in settings.properties to disable slow buildSearchableOptions while developing.
if (gradle.buildSearchableOptions) {
    buildSearchableOptions.enabled = false
}

task dvslBuild(type: GradleBuild) {
    tasks = ['copyPluginXml', ':inner-tool:buildInnerTool', 'buildPlugin']
}
